
###################### dotfiles ######################

# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/Code

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files except those on the enablers directory
for file in ${${(M)config_files:#*/path.zsh}:#$ZSH/enablers*}
do
  source $file
done

# load everything but the path and completion files and the enablers directory
for file in ${${${config_files:#*/path.zsh}:#*/completion.zsh}:#$ZSH/enablers*}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads except from the enablers directory
for file in ${${(M)config_files:#*/completion.zsh}:#$ZSH/enablers*}
do
  source $file
done

unset config_files


###################### oh-my-zsh ######################
#Theme
export ZSH_THEME="gnzh"

#Plugins
plugins=(git rbenv)
enablers/oh_my_zsh
source $ZSH/enablers/oh_my_zsh/oh-my-zsh.sh
